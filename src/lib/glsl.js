/** Shared GLSL chunks. Ashima simplex noise + helpers, kept in one place. */

export const SNOISE_2D = /* glsl */ `
vec3 mod289(vec3 x){ return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec2 mod289(vec2 x){ return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec3 permute(vec3 x){ return mod289(((x * 34.0) + 1.0) * x); }

float snoise(vec2 v){
  const vec4 C = vec4(0.211324865405187, 0.366025403784439,
                     -0.577350269189626, 0.024390243902439);
  vec2 i  = floor(v + dot(v, C.yy));
  vec2 x0 = v - i + dot(i, C.xx);
  vec2 i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;
  i = mod289(i);
  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0)) + i.x + vec3(0.0, i1.x, 1.0));
  vec3 m = max(0.5 - vec3(dot(x0, x0), dot(x12.xy, x12.xy), dot(x12.zw, x12.zw)), 0.0);
  m = m * m; m = m * m;
  vec3 x = 2.0 * fract(p * C.www) - 1.0;
  vec3 h = abs(x) - 0.5;
  vec3 ox = floor(x + 0.5);
  vec3 a0 = x - ox;
  m *= 1.79284291400159 - 0.85373472095314 * (a0 * a0 + h * h);
  vec3 g;
  g.x  = a0.x  * x0.x  + h.x  * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}
`

/** Cheap pseudo-3D noise assembled from three 2D slices. */
export const NOISE_3D = /* glsl */ `
float noise3(vec3 p){
  return ( snoise(p.xy + p.z * 0.71)
         + snoise(p.yz + p.x * 0.71)
         + snoise(p.zx + p.y * 0.71) ) * 0.3333333;
}
`

export const FBM = /* glsl */ `
float fbm(vec2 p){
  float v = 0.0;
  float a = 0.5;
  mat2 rot = mat2(0.80, 0.60, -0.60, 0.80);
  for (int i = 0; i < 5; i++) {
    v += a * snoise(p);
    p = rot * p * 2.03;
    a *= 0.5;
  }
  return v;
}
`

/** Ordered-ish dither. Dark gradients band badly on 8-bit displays without it. */
export const DITHER = /* glsl */ `
float hash12(vec2 p){
  vec3 p3 = fract(vec3(p.xyx) * 0.1031);
  p3 += dot(p3, p3.yzx + 33.33);
  return fract((p3.x + p3.y) * p3.z);
}
`

/**
 * Three-octave fbm with a fast amplitude falloff. Deliberately low-detail —
 * used for the hero field, where broad soft forms read better behind type
 * than the fine turbulence a five-octave stack produces.
 */
export const FBM_SOFT = /* glsl */ `
float fbmSoft(vec2 p){
  float v = 0.0;
  float a = 0.58;
  mat2 rot = mat2(0.80, 0.60, -0.60, 0.80);
  for (int i = 0; i < 3; i++) {
    v += a * snoise(p);
    p = rot * p * 2.0;
    a *= 0.45;
  }
  return v;
}
`
